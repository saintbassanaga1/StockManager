import org.gradle.api.plugins.internal.DefaultJavaPluginExtension

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'maven-publish'
    id "com.github.hierynomus.license" version "0.15.0"
}

group = 'tech.saintbassanaga'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    from('LICENSE') {
        into 'META-INF'
    }
}

bootJar {
    from('LICENSE') {
        into 'META-INF'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'tech.saintbassanaga'
            artifactId = 'StockManager'
            version = '1.0-SNAPSHOT'
            from components.java
            pom {
                name.set('Stock Manager')
                description.set('The stock manager project is a web-based application for tracking inventory, managing stock levels, and generating reports')
                url.set('https://github.com/saintbassanaga/StockManager')
                licenses {
                    license {
                        name.set('The MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                        distribution.set('repo')
                    }
                }
                developers {
                    developer {
                        id.set('saintbassanaga')
                        name.set('Saint Paul Bassanaga')
                        email.set('saintbassanaga01@icloud.com')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/saintbassanaga/StockManager.git')
                    developerConnection.set('scm:git:ssh://github.com:saintbassanaga/StockManager.git')
                    url.set('https://github.com/saintbassanaga/StockManager.git')
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/saintbassanaga/StockManager")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}

license {
    header = rootProject.file('LICENSE')
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}